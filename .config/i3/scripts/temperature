#!/usr/bin/env perl
# Copyright 2014 Pierre Mavro <deimos@deimos.fr>
# Copyright 2014 Vivien Didelot <vivien@didelot.org>
# Copyright 2014 Andreas Guldstrand <andreas.guldstrand@gmail.com>
# Copyright 2014 Benjamin Chretien <chretien at lirmm dot fr>

# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.

# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.

# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.
#
# Edited by Andreas Lindlbauer <endeavouros.mousily@aleeas.com>

use strict;
use warnings;
use utf8;
use Getopt::Long;

binmode(STDOUT, ":utf8");

# Default values
my $t_warn = $ENV{T_WARN}  || 70;
my $t_crit = $ENV{T_CRIT}  || 90;
my $chip   = $ENV{SENSOR_CHIP} || '';
my $temperature = -9999;

sub help {
    print <<'USAGE';
Usage: temperature [-w <warning>] [-c <critical>] [--chip <chip>]
  -w <percent>: warning threshold to become yellow
  -c <percent>: critical threshold to become red
  --chip <chip>: sensor chip
USAGE
    exit 0;
}

GetOptions(
    "help|h" => \&help,
    "w=i"    => \$t_warn,
    "c=i"    => \$t_crit,
    "chip=s" => \$chip,
    );

# Ensure 'sensors' exists
system("command -v sensors >/dev/null 2>&1") == 0
    or die "Error: 'sensors' command not found.\n";

# Get chip temperature
my @cmd = ("sensors", "-u");
push @cmd, $chip if length $chip;

open my $sensors, "-|", @cmd
    or die "Failed to run sensors: $!";

while (<$sensors>) {
    if (/^\s*temp\d+_input:\s*\+?(-?\d+(?:\.\d+)?)/) {
        $temperature = $1;
        last;
    }
}
close $sensors;

$temperature == -9999 and die "Cannot find temperature\n";

$temperature = int($temperature + 0.5);

# Pick label
my $label;
if    ($temperature < 45) { $label = ''; }
elsif ($temperature < 55) { $label = ''; }
elsif ($temperature < 65) { $label = ''; }
elsif ($temperature < 75) { $label = ''; }
else                      { $label = ''; }

# Output for i3blocks
my $output = "$label $temperature°C";
print "$output\n$output\n";

if    ($temperature >= $t_crit) { print "#FF0000\n"; exit 33; }
elsif ($temperature >= $t_warn) { print "#FFFC00\n"; }

exit 0;
